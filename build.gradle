import org.apache.tools.ant.filters.ReplaceTokens
import org.yaml.snakeyaml.Yaml

apply plugin: 'java'
apply plugin: 'nexus'
apply plugin: 'shadow'
apply plugin: 'rebel'

Yaml yaml = new Yaml()
ext.plugin = yaml.load(new FileInputStream(new File(projectDir, "src/main/resources/plugin.yml")))

jar.baseName = plugin.name

defaultTasks 'clean', 'build', 'shadow'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        ivy {
            url 'http://dl.bintray.com/content/johnrengelman/gradle-plugins'
        }
    }

    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.5'
        classpath group: 'org.gradle.api.plugins', name: 'gradle-nexus-plugin', version: '0.7'
        classpath group: 'org.zeroturnaround', name: 'gradle-jrebel-plugin', version: '1.1.2'
        classpath 'org.gradle.plugins:shadow:0.7'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'http://repo.md-5.net/content/groups/public/'
    }
}

dependencies {
    compile 'org.bukkit:bukkit:1.7.9-R0.3-SNAPSHOT'
    compile fileTree(dir: 'libs', includes: ['*.jar']) 
}

def cmd(str) {
    try {
        def output = new ByteArrayOutputStream()
        exec {
            commandLine str.split(' ')
            standardOutput = output
        }
        return output.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

jar.dependsOn(generateRebel)

processResources {
    from('src/main/resources') {
        include '*'
        filter { String line -> line.replace('$version', 'v' + cmd('git describe --always --dirty=-dirty')) }
    }
}

shadow {
    artifactAttached = false
    outputFile = jar.archivePath
    artifactSet {
        exclude '*'
    }
}

task scaffold << {
    // Make artifact source directory
    def pkg = plugin.main.substring(0, plugin.main.lastIndexOf('.'))
    def mainClass = plugin.main.substring(plugin.main.lastIndexOf('.') + 1, plugin.main.length())
    def artifactPath = new File('src/main/java/' + plugin.main.replace('.', '/')).getAbsolutePath()
    def artifactSrcDir = new File(artifactPath.substring(0, artifactPath.lastIndexOf(File.separator)))
    artifactSrcDir.mkdirs()
    
    // Copy plugin bootstrap file over
    copy {
        from '.bootstrap/'
        into artifactSrcDir.getPath()
        include 'PluginBootstrap.java'
        rename '.*', plugin.name + '.java'
        
        filter(ReplaceTokens, tokens: [pkg: pkg, pluginName: mainClass])
    }
    
    if ((cmd('git remote -v') =~ /origin.+?bukkit-bootstrap\.git/).find()) {
        cmd('git remote rename origin bukkit-bootstrap')
    }
}

task testCopy << {
    cmd("mkdir -p $testPluginDir")
    cmd("cp $projectDir/build/libs/${jar.baseName}.jar $testPluginDir")
}

task remoteCopy << {
    cmd("scp $projectDir/build/libs/${jar.baseName}.jar $remotePluginDir")
}

modifyPom {
    project {
        name plugin.name
        description plugin.description
        url plugin.website
    }
}

nexus {
    attachSources = true
    attachTests = true
    attachJavadoc = false
    sign = true
    if (hasProperty('deployRepoUrl')) repositoryUrl = deployRepoUrl
    if (hasProperty('deploySnapshotRepoUrl')) snapshotRepositoryUrl = deploySnapshotRepoUrl
}
