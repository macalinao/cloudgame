/**
 * bukkit-bootstrap (c) 2013-2014 Ian Macalinao
 */
import org.yaml.snakeyaml.Yaml
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

// Plugin information loaded from the YML file
ext.plugin = (new Yaml()).load(new FileInputStream(new File(projectDir, "src/main/resources/plugin.yml")))

// Determine the group/artifact
ext.pkg = plugin.main.substring(0, plugin.main.lastIndexOf('.'))

group = pkg.substring(0, plugin.main.lastIndexOf('.'))
archivesBaseName = plugin.name.toLowerCase()

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'org.yaml:snakeyaml:1.5'
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
  }
}

repositories {
  mavenCentral()
  maven {
    url 'http://repo.md-5.net/content/groups/public/'
  }
}

dependencies {
  compile 'org.bukkit:bukkit:1.7.9-R0.3-SNAPSHOT'
  compile fileTree(dir: 'libs', includes: ['*.jar']) 
}

processResources {
  from('src/main/resources') {
    include '*'
    filter { String line ->
      line.replace('$version', 'v' + cmd('git describe --always --dirty=-dirty'))
    }
  }
}

def cmd(str) {
  try {
    def output = new ByteArrayOutputStream()
    exec {
      commandLine str.split(' ')
      standardOutput = output
    }
    return output.toString().trim()
  }
  catch (ignored) {
    return null;
  }
}

/**
 * Scaffolds the Bukkit project.
 */
task scaffold << {
  // Make artifact source directory
  def mainClass = plugin.main.substring(plugin.main.lastIndexOf('.') + 1, plugin.main.length())
  def artifactPath = new File('src/main/java/' + plugin.main.replace('.', '/')).getAbsolutePath()
  def artifactSrcDir = new File(artifactPath.substring(0, artifactPath.lastIndexOf(File.separator)))
  artifactSrcDir.mkdirs()

  // Copy plugin bootstrap file over
  copy {
    from '.bootstrap/'
    into artifactSrcDir.getPath()
    include 'PluginBootstrap.java'
    rename '.*', plugin.name + '.java'

    filter(ReplaceTokens, tokens: [pkg: pkg, pluginName: mainClass])
  }

  if ((cmd('git remote -v') =~ /origin.+?bukkit-bootstrap\.git/).find()) {
    cmd('git remote rename origin bukkit-bootstrap')
  }
}

task testCopy << {
  cmd("mkdir -p $testPluginDir")
  cmd("cp $projectDir/build/${plugin.name}.jar $testPluginDir")
}

task remoteCopy << {
  cmd("scp $projectDir/build/${plugin.name}.jar $remotePluginDir")
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}

// Copy resulting JAR to a nice name
task copyJar(type: Copy) {
  from 'build/libs/'
  into 'build/'
  include jar.archiveName
  rename jar.archiveName, plugin.name + '.jar'
}

// Publishing stuff
task javadocJar(type: Jar) {
  classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name plugin.name
        packaging 'jar'
        description plugin.description
        url plugin.url
      }
    }
  }
}

// Task dependencies
build.dependsOn clean
copyJar.dependsOn build
testCopy.dependsOn copyJar
